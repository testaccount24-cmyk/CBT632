TOASCILF csect
TOASCILF amode 31
TOASCILF rmode 24

           #REGEQU

***********************************************************************
*                                                                     *
*   MODULE NAME:  TOASCILF - EBCDIC to ASCII                          *
*                                                                     *
***********************************************************************

         using TOASCILF,R12
         SAVE  (14,12),,*                   SAVE REGISTERS
         lr    R12,R15
         lr    R11,R13
         st    R13,SAVEAREA+4
         la    R13,SAVEAREA
         #JISTAMP
         b     INITRTN
SAVEAREA dc    18F'0'
INITRTN  equ   *
         st    R13,8(R11)

         mvc   WTOMSG(27),=c'** Opening SYSUT1        **'
         bal   R11,WTOPRTN

         OPEN  (SYSUT1,(INPUT))
         la    R2,SYSUT2
         la    R3,SYSUT1
         using IHADCB,R2
         mvc   DCBRECFM,DCBRECFM-IHADCB(R3)

         lh    R15,DCBPRECL-IHADCB(R3)
         ltr   R15,R15
         bz    DO$LRECL
         cvd   R15,DWORDIN
         ah    R15,=h'2'
         sth   R15,DCBPRECL
         cvd   R15,DWORDOUT

         oi    DWORDIN+7,x'0F'
         mvc   WTOMSG(27),=c'**  input PRECL = 00000  **'
         unpk  WTOMSG+18(5),DWORDIN+5(3)
         bal   R11,WTOPRTN

         oi    DWORDOUT+7,x'0F'
         mvc   WTOMSG(27),=c'** output PRECL = 00000  **'
         unpk  WTOMSG+18(5),DWORDOUT+5(3)
         bal   R11,WTOPRTN

DO$LRECL equ   *

         lh    R15,DCBLRECL-IHADCB(R3)
         cvd   R15,DWORDIN
         ah    R15,=h'2'
         sth   R15,DCBLRECL
         cvd   R15,DWORDOUT

         oi    DWORDIN+7,x'0F'
         mvc   WTOMSG(27),=c'**  input LRECL = 00000  **'
         unpk  WTOMSG+18(5),DWORDIN+5(3)
         bal   R11,WTOPRTN

         oi    DWORDOUT+7,x'0F'
         mvc   WTOMSG(27),=c'** output LRECL = 00000  **'
         unpk  WTOMSG+18(5),DWORDOUT+5(3)
         bal   R11,WTOPRTN

         sr    R14,R14
         lh    R15,DCBBLKSI-IHADCB(R3)
         cvd   R15,DWORDIN
         lh    R1,DCBLRECL-IHADCB(R3)
         dr    R14,R1
         ltr   R14,R14      If remainder not = 0 - then -
         bnz   BAD$BF        - we have a bad Blocking Factor
         mh    R15,DCBLRECL This is ( old LRECL +2 ) * Block Factor
         sth   r15,DCBBLKSI = new Block Size !
         cvd   R15,DWORDOUT

         oi    DWORDIN+7,x'0F'
         mvc   WTOMSG(27),=c'**  input BLKSZ = 00000  **'
         unpk  WTOMSG+18(5),DWORDIN+5(3)
         bal   R11,WTOPRTN

         oi    DWORDOUT+7,x'0F'
         mvc   WTOMSG(27),=c'** output BLKSZ = 00000  **'
         unpk  WTOMSG+18(5),DWORDOUT+5(3)
         bal   R11,WTOPRTN


         la    R2,SYSUT1

         mvc   WTOMSG(27),=c'** RECFM = variable      **'
         tm    DCBRECFM,DCBRECV
         bo    FILE$OKF

         mvc   WTOMSG(27),=c'** RECFM = fixed         **'
         tm    DCBRECFM,DCBRECF
         bo    FILE$OKV

         mvc   WTOMSG(27),=c'** RECFM = unknown       **'
         bal   R11,WTOPRTN
         l     R15,=f'16'
         b     CLOSE1

FILE$OKV       equ   *

         clc   DCBLRECL,=h'1026'
         bnh   FILE$OK

         mvc   WTOMSG(27),=c'** LRECL more than 1026   **'
         bal   R11,WTOPRTN
         l     R15,=f'16'
         b     CLOSE1

BAD$BF         equ   *

         lr    R3,R14
         st    R15,WORD
         cvd   R15,DWORD
         oi    DWORD+7,x'0F'
         mvc   WTOMSG(27),=c'**    Blocking  = 00000  **'
         unpk  WTOMSG+18(5),DWORD+5(3)
         bal   R11,WTOPRTN
         st    R3,WORD
         cvd   R15,DWORD
         oi    DWORD+7,x'0F'
         mvc   WTOMSG(27),=c'**   Remainder  = 00000  **'
         unpk  WTOMSG+18(5),DWORD+5(3)
         bal   R11,WTOPRTN
         mvc   WTOMSG(27),=c'**  Bad Blocking Factor  **'
         bal   R11,WTOPRTN
         l     R15,=f'16'
         b     CLOSE1

FILE$OKF       equ   *

         clc   DCBLRECL,=h'1022'
         bnh   FILE$OK

         mvc   WTOMSG(27),=c'** LRECL more than 1022   **'
         bal   R11,WTOPRTN
         l     R15,=f'16'
         b     CLOSE1

FILE$OK        equ   *

         bal   R11,WTOPRTN

         OPEN  (SYSUT2,(OUTPUT))
         la    R2,SYSUT2

READ$WRITE     equ   *

         l     R1,=A(SYSUT1)
         l     R0,=A(BUFF)
         GET   (1),(0)
         l     R14,=A(BUFF)

         lh    R1,DCBLRECL
         bctr  R1,R0

         lh    R3,DCBLRECL
         a     R3,=a(BUFF)
         sh    R3,=h'2'            <<<<<<- TESTING TESTING 1...2....3
         mvc   0(2,R3),=x'0D25'    <<<<<<- EBCDIC chars =  CRLF

*->>>>>> b     BYP$XLATE <<<<<<<<<<<<<<<<- Testing

         tm    DCBRECFM,DCBRECF
         bo    translate
         sh    R1,=h'4'           Don't translate the RDW
         ah    R14,=h'4'           start after the RDW

TRANSLATE      equ   *
               ch    R1,=h'255'
               bh    TRANSLATE256GT
               b     TRANSLATE256LE

TRANSLATE256GT equ   *
               lh    R15,=h'255'
               ex    R15,DOASCII
               sh    R1,=h'256'
               ah    R14,=h'256'
               b     TRANSLATE

TRANSLATE256LE ex    R1,DOASCII


BYP$XLATE equ   *

         l     R1,=A(SYSUT2)
         l     R0,=A(BUFF)
         PUT   (1),(0)

         b     READ$WRITE

NOMORE   equ   *

         mvc   WTOMSG(27),=c'** Issuing CLOSEs        **'
         bal   R11,WTOPRTN

         CLOSE SYSUT2

         sr    R15,R15

CLOSE1   equ   *

         CLOSE SYSUT1

         l     R13,SAVEAREA+4
         RETURN (14,12),RC=(15)

DOASCII  tr    0(0,R14),ASCII      EBCDIC ---->  ASCII

*
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
*------------> End of Main-line Code <--------------------------------*
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
*
*
WTOPRTN   equ   *
          la    R4,WTOLIST
          WTO   MF=(E,(R4))
          mvi   WTOMSG,C' '
          mvc   WTOMSG+1(L'WTOMSG-1),WTOMSG
          br    R11

WTOLIST  WTO '                                                         X
                              ',                                       X
               ROUTCDE=11,DESC=7,MF=L
WTOMSG    equ   WTOLIST+4,70

*---------------------------------------------------------------------*
*---------------------------------------------------------------------*


DWORD    DC    d'0'
DWORDIN  DC    d'0'
DWORDOUT DC    d'0'
WORD     DC    f'0'


EBCDIC   DC    256CL1' '   x'40' - space
*
         ORG   EBCDIC+X'20'
         DC    X'405A7F7B5B6C507D'
         DC    C'()*+,-./0123456789:;< >?@'
         DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ{\}Â¬_`'
         DC    C'abcdefghijklmnopqrstuvwxyz{ }~'
*
         ORG

*                000102030405060708090A0B0C0D0E0F
ASCII    DC    x'000102031A091A7F1A1A1A0B0C0D0E0F'   00
         DC    x'101112131A1A081A18191A1A1C1D1E1F'   10
         DC    x'1A1A1C1A1A0A171B1A1A1A1A1A050607'   20
         DC    x'1A1A161A1A1E1A041A1A1A1A14151A1A'   30
         DC    x'20A6E180EB909FE2AB8B9B2E3C282B7C'   40
         DC    x'26A9AA9CDBA599E3A89E21242A293B5E'   50
         DC    x'2D2FDFDC9ADDDE989DACBA2C255F3E3F'   60
         DC    x'D78894B0B1B2FCD6FB603A2340273D22'   70
         DC    x'F861626364656667686996A4F3AFAEC5'   80
         DC    x'8C6A6B6C6D6E6F7071729787CE93F1FE'   90
         DC    x'C87E737475767778797AEFC0DA5BF2F9'   A0
         DC    x'B5B6FDB7B8B9E6BBBCBD8DD9BF5DD8C4'   B0
         DC    x'7B414243444546474849CBCABEE8ECED'   C0
         DC    x'7D4A4B4C4D4E4F505152A1ADF5F4A38F'   D0
         DC    x'5CE7535455565758595AA0858EE9E4D1'   E0
         DC    x'30313233343536373839B3F7F0FAA7FF'   40

ASCII2   DC    256XL1'20' - space
         ORG   ASCII+X'40'   SPace
         DC    X'20'
         ORG   ASCII+C'a'
         DC    X'616263646566676869'
         ORG   ASCII+C'j'
         DC    X'6A6B6C6D6E6F707172'
         ORG   ASCII+C's'
         DC    X'737475767778797A'
         ORG   ASCII+C'A'
         DC    X'414243444546474849'
         ORG   ASCII+C'J'
         DC    X'4A4B4C4D4E4F505152'
         ORG   ASCII+C'S'
         DC    X'535455565758595A'
         ORG   ASCII+C'0'
         DC    X'30313233343536373839'
         ORG


         ltorg

         ds    0D
BUFF     ds    0cl1028
         dc    1028cl1' '


SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GM,                        X
               EODAD=NOMORE
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM,LRECL=100

          DCBD  DSORG=(PS),DEVD=DA

         end   TOASCILF
